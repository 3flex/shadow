import org.gradle.wrapper.Download
import org.gradle.wrapper.GradleUserHomeLookup
import org.gradle.wrapper.Install
import org.gradle.wrapper.PathAssembler
import org.gradle.wrapper.WrapperConfiguration
import org.gradle.wrapper.WrapperExecutor
import org.gradle.wrapper.Logger

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:3.0.0"
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.11'
        classpath 'org.asciidoctor:asciidoctorj-groovy-dsl:1.0.0.preview2'
        classpath "com.bluepapa32:gradle-watch-plugin:0.1.5"
        classpath "org.kordamp.gradle:livereload-gradle-plugin:0.2.1"
        classpath 'org.ajoberstar:gradle-git:1.5.0'
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.0'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'groovy'
    id 'project-report'
    id 'idea'
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

apply from: file('gradle/docs.gradle')
apply from: file('gradle/publish.gradle')
apply from: file('gradle/asciidoc.gradle')
apply from: file('gradle/ghPages.gradle')

repositories {
    mavenLocal()
    jcenter()
}

dependencies {

    shadow localGroovy()
    shadow gradleApi()
    compile 'org.jdom:jdom2:2.0.5'
    compile 'org.ow2.asm:asm:5.0.3'
    compile 'org.ow2.asm:asm-commons:5.0.3'
    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.ant:ant:1.9.4'
    compile 'org.codehaus.plexus:plexus-utils:2.0.6'
    compile 'org.codehaus.groovy:groovy-backports-compat23:2.4.4'


    testCompile gradleTestKit()
    testCompile("org.spockframework:spock-core:1.0-groovy-2.4") {
        exclude module: 'groovy-all'
    }
    testCompile 'xmlunit:xmlunit:1.3'
    testCompile 'commons-lang:commons-lang:2.6'
    testCompile 'com.google.guava:guava:17.0'
}

test {
    dependsOn publishToMavenLocal
    if (System.env.CI == 'true') {
        testLogging.showStandardStreams = true
    }
}

jar {
    from rootProject.file('LICENSE')
    from rootProject.file('NOTICE')
}

shadowJar {
    classifier = ''
    [
            'org.codehaus.plexus',
            'org.jdom',
            'org.objectweb.asm'
    ].each {
        relocate it, "shadow.$it"
    }
}

tasks.withType(AbstractCompile) {
    classpath += project.configurations.shadow
}

idea {
    project {
        languageLevel = '1.6'
    }
}

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

task installWrappers << {
    WrapperExecutor wrapperExecutor = WrapperExecutor.forWrapperPropertiesFile(file('gradle/wrapper/gradle-wrapper.properties'), System.out)
    ['1.12', '2.0', '2.5', '2.11', '2.12', '2.13', '2.14', '3.0-milestone-2'].each {
        WrapperConfiguration config = wrapperExecutor.config
        config.setDistribution(new URI("https://services.gradle.org/distributions/gradle-$it-bin.zip"))
        Install install = new Install(new Logger(true), new Download(new Logger(true), "gradlew", it),
                new PathAssembler(GradleUserHomeLookup.gradleUserHome()))
        install.createDist(config)
    }
}
