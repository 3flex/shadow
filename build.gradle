buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

task integTest(type: GradleBuild) {
    buildFile = file('it/build.gradle')
    dir = file('it')
    tasks = ['clean', 'integTest']
    startParameter.projectProperties.currentVersion = currentVersion
}
integTest.dependsOn ':shadow:publishToMavenLocal'

tasks.addRule('Pattern: integTest<Project>') { String taskName ->
    if (taskName.startsWith('integTest')) {
        def projectName = taskName - 'integTest'
        task "$taskName"(type: GradleBuild, dependsOn: ':shadow:publishToMavenLocal') {
            buildFile = file('it/build.gradle')
            dir = file('it')
            tasks = [":$projectName:clean", ":$projectName:integTest"]*.toString()
            startParameter.projectProperties.currentVersion = currentVersion
        }
    }
}

task test(dependsOn: integTest)
subprojects {
    tasks.withType(Test) {
        rootProject.tasks.test.dependsOn it
        rootProject.tasks.integTest.mustRunAfter it
    }
}

allprojects {
    group = 'com.github.jengelman.gradle.plugins'
    version = "${currentVersion}"
}

ext.isSnapshot = version.endsWith("SNAPSHOT")
