buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'project-report'

apply from: file('gradle/docs.gradle')
apply from: file('gradle/publish.gradle')

group = 'com.github.jengelman.gradle.plugins'
version = '0.9.0'

ext.isSnapshot = version.endsWith("SNAPSHOT")

repositories {
    jcenter()
    maven {
        url 'http://dl.bintray.com/johnrengelman/gradle-plugins'
    }
}

sourceSets {
    testUtils
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'jdom:jdom:1.1'
    compile 'asm:asm:3.3.1'
    compile 'asm:asm-commons:3.3.1'
    compile 'org.apache.ant:ant:1.9.3'
    compile 'org.apache.commons:commons-io:1.3.2'

    testUtilsCompile localGroovy()
    testUtilsCompile gradleApi()

    testCompile configurations.testUtilsCompile
    testCompile 'com.github.jengelman.gradle.plugins:gradle-test-kit:0.2'
    testCompile("org.spockframework:spock-core:0.7-groovy-1.8") {
        exclude module: 'groovy-all'
    }
    testCompile 'xmlunit:xmlunit:1.3'
}

jar {
    from rootProject.file('LICENSE')
    from rootProject.file('NOTICE')
}

idea {
    module {
        testSourceDirs += sourceSets.testUtils.allSource.srcDirs
        scopes.TEST.plus += configurations.testUtilsRuntime
    }
}

sourceSets.test.compileClasspath += sourceSets.testUtils.output
sourceSets.test.runtimeClasspath += sourceSets.testUtils.output

