apply plugin: 'bintray'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'

repositories {
    jcenter()
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'jdom:jdom:1.1'
    compile 'asm:asm:3.3.1'
    compile 'asm:asm-commons:3.3.1'
    
    testCompile "org.spockframework:spock-core:0.7-groovy-1.8"
    testCompile 'xmlunit:xmlunit:1.3'
}

def javaApiUrl = 'http://docs.oracle.com/javase/1.7.0/docs/api'
def groovyApiUrl = 'http://groovy.codehaus.org/gapi/'

tasks.withType(Javadoc) {
    options.links(javaApiUrl, groovyApiUrl)
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

def pomConfig = {
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'jengelman'
            name 'John Engelman'
            email 'john.r.engelman@gmail.com'
        }
    }
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Gradle plugin to combine dependencies into a single Jar. Port of Maven Shade.')
                root.children().last() + pomConfig
            }
        }
    }
}

task configureBintray << {
    bintrayUpload.user = bintrayUser
    bintrayUpload.apiKey = bintrayKey
}

bintrayUpload.dependsOn configureBintray, project.publishing.publications.plugin.artifacts

bintray {
    publications = ['plugin']
    pkg {
        repo = 'gradle-plugins'
        name = 'gradle-shadow-plugin'
        licenses = ['Apache-2.0']
    }
}

task install(dependsOn: publishToMavenLocal)

build.dependsOn javadocJar, sourcesJar

jar {
    from rootProject.file('LICENSE')
    from rootProject.file('NOTICE')

}
