dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'jdom:jdom:1.1'
    compile 'asm:asm:3.3.1'
    compile 'asm:asm-commons:3.3.1'
    
    testCompile "org.spockframework:spock-core:0.7-groovy-1.8"
    testCompile 'xmlunit:xmlunit:1.3'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

task configureUploadArchivesBinTray << {
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(
                        url: bintray_api_base_url + '/maven/' + bintray_username + '/' + bintray_repo + '/' + bintray_package
                ) {
                    authentication(
                            userName: bintray_username,
                            password: bintray_api_key
                    )
                }
            }
        }
    }
}

configureUploadArchivesBinTray.onlyIf {
    !isSnapshot()
}

task configureUploadArchivesArtifactory << {
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "${bloom_artifactory_contextUrl}/libs-${isSnapshot() ? 'snapshot' : 'release'}-local") {
                    authentication(
                            userName: bloom_artifactory_username,
                            password: bloom_artifactory_password
                    )
                }
            }
        }
    }
}

task configureUploadArchives(dependsOn: [configureUploadArchivesBinTray, configureUploadArchivesArtifactory])

uploadArchives.dependsOn test, configureUploadArchives

jar {
    baseName = 'shadow'
    from 'LICENSE'
    from 'NOTICE'
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.artifactId = 'shadow'
        }
    }
}
